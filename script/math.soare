
?
?  _____  _____  ___  ______ _____
? /  ___||  _  |/ _ \ | ___ \  ___|
? \ `--. | | | / /_\ \| |_/ / |__
?  `--. \| | | |  _  ||    /|  __|
? /\__/ /\ \_/ / | | || |\ \| |___
? \____/  \___/\_| |_/\_| \_\____/
?
? Antoine LANDRIEUX (MIT License) <math.soare>
? <https://github.com/AntoineLandrieux/SOARE/>
?
? Included Constants:
?   - pi
?   - deg
?   - rad
?   - neg
?
? Included Functions:
?   - NaN
?   - abs
?   - not
?   - floor
?   - integer_division
?   - pow
?   - factorial
?   - cos
?   - sin
?   - tan

let pi  = 3.14159265;
let deg = 180 / pi;
let rad = pi / 180;
let neg = 0 - 1;

?
? Returns true if a value is Not-a-Number
?
fn NaN(x)
  let i = 0;
  try
    if (x:0 == '-' || x:0 == '+')
      i = 1;
    end;
  end;
  while (1)
    try
      let num = ord(x:i);
      if (num >= ord('0') && num <= ord('9'))
        i = i + 1;
      else
        return 1;
      end;
    iferror
      break;
    end;
  end;
  return 0;
end;

?
? Absolute value (|x|)
?
fn abs(x)
  if (x < 0)
    return x * neg;
  end;
  return x;
end;

?
? Invert the truth value of Boolean expressions
?
fn not(x)
  return x == 0;
end;

?
? Floating point to integer conversion
?
fn floor(float)
  return float^0;
end;

?
? Integer division
?
fn integer_division(x; y)
  let res = 0;

  while (x >= y)
    x = x - y;
    res = res + 1;
  end;

  return res;
end;

?
? Return the nearest integer
?
fn round(x)
  let sign = x < 0;
  let y = abs(x);
  let t = floor(y);

  if (sign)
    sign = neg;
  else
    sign = 1;
  end;

  if (y - t >= 0.5)
    return sign * (t + 1);
  end;

  return sign * t;
end;

?
? Returns the value of x to the power of y
?
fn pow(x; y)
  let res = 1;
  let ltz = y < 0;

  y = floor(abs(y));

  while (y)
    res = res * x;
    y = y - 1;
  end;

  if (ltz)
    res = 1 / res;
  end;

  return res;
end;

?
? The factorial of a non-negative integer
?
fn factorial(x)
  let res = 1;
  let n = 1;

  x = floor(x);

  if (x < 0)
    raise "Undefined";
  end;

  while (n < x + 1)
    res = res * n;
    n = n + 1;
  end;

  return res;
end;

?
? Convert Degrees to Radians
?
fn radians(x)
  return x * rad;
end;

?
? Convert Radians to Degrees
?
fn degrees(x)
  return x * deg;
end;

?
? The sine function (deg)
?
fn sin(x)
  let sine = 0;
  let n = 0;

  x = radians(x % 180)

  while (n < 10)
    sine = sine + pow(neg; n) * pow(x; 2 * n + 1) / factorial(2 * n + 1);
    n = n + 1;
  end;

  return sine;
end;

?
? The cosine function (deg)
?
fn cos(x)
  let cosine = 0;
  let n = 0;

  x = radians(x % 180);

  while (n < 10)
    cosine = cosine + pow(neg; n) * pow(x; 2 * n) / factorial(2 * n);
    n = n + 1;
  end;

  return cosine;
end;

?
? The tangent function (deg)
?
fn tan(x)
  return sin(x) / cos(x);
end;
