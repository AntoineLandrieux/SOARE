
?
?  _____  _____  ___  ______ _____
? /  ___||  _  |/ _ \ | ___ \  ___|
? \ `--. | | | / /_\ \| |_/ / |__
?  `--. \| | | |  _  ||    /|  __|
? /\__/ /\ \_/ / | | || |\ \| |___
? \____/  \___/\_| |_/\_| \_\____/
?
? Antoine LANDRIEUX (MIT License) <math.soare>
? <https://github.com/AntoineLandrieux/SOARE/>
?
? Included Variables:
?   - pi
?   - deg
?   - rad
?
? Included Functions:
?   - M
?   - Math
?   - NaN
?   - abs
?   - not
?   - floor
?   - integer_division
?   - integer_sqrt
?   - pow
?   - factorial
?   - cos
?   - sin
?   - tan

$ pi  = 3.14159265;
$ deg = 180 / pi;
$ rad = pi / 180;

? @brief Useful for math, ex: 5*M(1+2)
@M(f)
  return f;
end;

? @brief Useful for math, ex: 5*M(1+2)
@Math(f)
  return f*1;
end;

? @brief Returns true if a value is Not-a-Number
@NaN(x)
  try
    x = x * 1;
  iferror
    return 1;
  end;
  return 0;
end;

? Absolute value
@abs(x)
  if x < 0 do
    return x*M(0-1);
  end;
  return Math(x);
end;

? Invert the truth value of Boolean expressions
@not(x)
  return x == 0;
end;

? @brief Floating point to integer conversion
@floor(float)
  return float^0;
end;

? @brief Integer division
@integer_division(x; y)
  $ res = 0;
  while x >= y do
    x = x - y;
    res = res + 1;
  end;
  return res;
end;

@round(x)
  $ sign = x < 0;
  $ y = abs(x);
  $ t = floor(y);

  if sign do
    sign = 0 - 1;
  else
    sign = 1;
  end;

  if y - t >= 0.5 do
    return sign * M(t + 1);
  end;

  return sign * t;
end;

? @brief Integer square root
@integer_sqrt(x)
  x = round(x);

  if x == 0 do
    return 0;
  or x < 0 do
    raise "Non-Real Number";
  end;
  
  $ n = x;
  $ a = 0;

  while 1 do
    a = integer_division(n + integer_division(x; n); 2)
    if a == n do
      return n;
    end;
    n = a;
  end;
end;

? @brief Returns the value of x to the power of y
@pow(x; y)
  $ res = 1;
  $ neg = y < 0;
  y = floor(abs(y))

  while y do
    res = res * x;
    y = y - 1;
  end;
  
  if neg do
    res = 1 / res;
  end;
  return res;
end;    

? @brief The factorial of a non-negative integer
@factorial(x)
  $ res = 1;
  $ n   = 1;

  x = floor(x);
  if x < 0 do
    raise "Undefined";
  end;

  while n < x + 1 do
    res = res * n;
    n = n + 1;
  end;
    
  return res;
end;

? @brief Convert Degrees to Radians
@radians(x)
  return x * rad;
end;

? @brief Convert Radians to Degrees
@degrees(x)
  return x * deg;
end;

? @brief The sine function
@sin(x)
  $ sine = 0;
  $ n    = 0;

  x = radians(x % 180)

  while n < 10 do
    sine = sine + pow(0-1; n) * pow(x; 2 * n + 1) / factorial(2 * n + 1);
    n = n + 1;
  end;
    
  return sine;
end;

? @brief The cosine function
@cos(x)
  $ cosine = 0;
  $ n      = 0;

  x = radians(x % 180)

  while n < 10 do
    cosine = cosine + pow(0-1; n) * pow(x; 2 * n) / factorial(2 * n);
    n = n + 1;
  end;
    
  return cosine;
end;

? @brief The tangent function
@tan(x)
  return sin(x) / cos(x);
end;
